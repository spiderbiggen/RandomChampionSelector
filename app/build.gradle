apply plugin: "com.android.application"
apply plugin: "dagger.hilt.android.plugin"
apply from: "../config-android.gradle"

android {
    namespace "com.spiderbiggen.randomchampionselector"
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                Properties keystoreProperties = new Properties()
                keystoreProperties.load(keystorePropertiesFile.newDataInputStream())

                keyAlias = keystoreProperties["keyAlias"]
                keyPassword = keystoreProperties["keyPassword"]
                storeFile = file(keystoreProperties["storeFile"])
                storePassword = keystoreProperties["storePassword"]
            } else {
                def path = System.getenv("SIGNING_STORE_PATH")
                if (path.length > 0) {
                    storeFile = file(path)
                    storePassword System.getenv("SIGNING_STORE_PASSWORD")
                    keyAlias System.getenv("SIGNING_KEY_ALIAS")
                    keyPassword System.getenv("SIGNING_KEY_PASSWORD")
                }
            }
        }
    }
    defaultConfig {
        applicationId Config.applicationId
        versionCode Config.versionCode
        versionName Config.versionName

        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    implementation project(":presentation")
    implementation project(":domain")
    implementation project(":data")

    implementation Dependencies.kotlin

    implementation Dependencies.appCompat
    implementation Dependencies.preferenceKtx

    implementation Dependencies.daggerHilt
    //Compile time dependencies
    kapt Dependencies.daggerHiltCompiler
    kapt Dependencies.daggerHiltJetpackCompiler
}

kapt {
    javacOptions {
        //https://github.com/google/dagger/issues/970
        option("-Adagger.validateTransitiveComponentDependencies=DISABLED")
    }
}

