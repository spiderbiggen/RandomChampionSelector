// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath BuildPlugins.android
        classpath BuildPlugins.kotlin
        classpath BuildPlugins.hilt
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.42.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

// JVM target applied to all Kotlin tasks across all subprojects
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    //Important: keep jvmTarget in sync with root/config-android.gradle
}

def isNonStable = { version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    checkForGradleUpdate = true
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}